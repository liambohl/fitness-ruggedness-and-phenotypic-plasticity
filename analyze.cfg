###########################################
# use DETAIL, TRACE, and PRINT to analyze
# final dominant genotypes and their
# performance in both environments
###########################################
# d: base directory
SET d /mnt/home/bohllia1/fitness-ruggedness
# u: final update
SET u 100000
###########################################

SET_BATCH 0

# static environment treatment
SET t Static
FORRANGE i 1 10
	PURGE_BATCH 0

	LOAD $d/data/$t_$i/data/detail-$u.spop
	FIND_GENOTYPE num_cpus
	PRINT analysis/$t_$i/final_dom

	SET v nand_1_not_1
	SetReactionValue NAND 1.0
	SetReactionValue NOT 1.0

	RECALC
	DETAIL analysis/$t_$i/final_dom/env_$v/final_dominant.dat update_born parent_dist fitness length sequence total_cpus num_cpus gest_time efficiency task.0 task.1
	TRACE analysis/$t_$i/final_dom/env_$v
END


# changing environment treatments
FOREACH t Two-envs Two-envs-sense
	# for each trial
	FORRANGE i 1 10
		PURGE_BATCH 0

		LOAD $d/data/$t_$i/data/detail-$u.spop
		FIND_GENOTYPE num_cpus
		PRINT analysis/$t_$i/final_dom

		##### nand_1_not_-1 tests #####

		SET v nand_1_not_-1
		SetReactionValue NAND 1.0
		SetReactionValue NOT -1.0

		RECALC
		DETAIL analysis/$t_$i/final_dom/env_$v/final_dominant.dat update_born parent_dist fitness length sequence total_cpus num_cpus gest_time efficiency task.0 task.1
		TRACE analysis/$t_$i/final_dom/env_$v

		##### nand_-1_not_1 tests #####

		SET v nand_-1_not_1
		SetReactionValue NAND -1.0
		SetReactionValue NOT 1.0

		RECALC
		DETAIL analysis/$t_$i/final_dom/env_$v/final_dominant.dat update_born parent_dist fitness length sequence total_cpus num_cpus gest_time efficiency task.0 task.1
		TRACE analysis/$t_$i/final_dom/env_$v
	END
END
